apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply from: 'installation/apply-local.groovy'

configurations {
	releaseArtifacts {}
	installers {}
	deployerJars {}
}

repositories {
	mavenCentral()
	mavenRepo url: 'http://m2repo.spockframework.org/snapshots'
}

dependencies {
	compile gradleApi(),
			'org.ajoberstar:gradle-git:0.4.0'
	groovy localGroovy()
	testCompile('org.spockframework:spock-core:0.5-groovy-1.8') { exclude group: 'org.codehaus.groovy' }
	testCompile 'junit:junit:4.10'
	deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

// This updates the apply script with the new version.
updateVersion << {
	def applyFile = new File("installation", "apply.groovy")
	def oldVersion = project.getProperty("release.oldVersion")
	def newVersion = project.getProperty("release.newVersion")
	ant.replace(file: applyFile, token: "${group}:${name}:${oldVersion}", value: "${group}:${name}:${newVersion}")
}



uploadReleaseArtifacts {
	repositories {
		mavenDeployer {
			//configuration = configurations.deployerJars
			//repository(url: releaseRepoUrl) { authentication(userName: releaseRepoUser, password: releaseRepoPass) }

			beforeDeployment { MavenDeployment deployment -> signPom(deployment) }
			repository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
				authentication(userName: releaseRepoUser, password: releaseRepoPass)
			}
			pom.project {
				name 'gradle-release'
				packaging 'jar'
				description 'gradle-release is a plugin for providing a Maven-like release process to project using Gradle.'
				url 'https://github.com/townsfolk/gradle-release'

				scm {
					url 'scm:git@github.com:townsfolk/gradle-release.git'
					connection 'scm:git@github.com:townsfolk/gradle-release.git'
					developerConnection 'scm:git@github.com:townsfolk/gradle-release.git'
				}

				licenses {
					license {
						name 'The MIT License (MIT)'
						url 'http://opensource.org/licenses/MIT'
						distribution 'repo'
					}
				}

				developers {
					developer {
						id 'townsfolk'
						name 'Eric Berry'
					}
				}
			}
		}
	}
}

uploadInstallers {
	repositories {
		add(new org.apache.ivy.plugins.resolver.SFTPResolver()) {
			host = releaseRepoHost
			addArtifactPattern "${releaseRepoBase}/[module]/[revision]/[artifact].[ext]"
		}
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	releaseArtifacts jar
	releaseArtifacts javadocJar
	releaseArtifacts sourcesJar
	installers file('installation/apply.groovy')
}

signing {
	sign configurations.releaseArtifacts
}

createReleaseTag.dependsOn uploadReleaseArtifacts
createReleaseTag.dependsOn uploadInstallers